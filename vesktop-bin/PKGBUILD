# Maintainer: zxp19821005 <zxp19821005 at 163 dot com>
# Contributor: HentaiShiroYuki <hentaishiroyuki at gmail dot com>
# Contributor: Sofia Lima
pkgname=vesktop-bin
_appname=vencord-desktop
pkgver=1.6.1
pkgrel=1
pkgdesc="A cross platform electron-based desktop app aiming to give you a snappier Discord experience with Vencord pre-installed."
arch=(
    'aarch64'
    'x86_64'
)
url="https://github.com/Vencord/Vesktop"
license=('GPL-3.0-only')
provides=(
    "${pkgname%-bin}=${pkgver}"
    "${_appname}=${pkgver}"
)
conflicts=(
    "${pkgname%-bin}"
    "${_appname}"
)
optdepends=(
    'speech-dispatcher: High-level device independent layer for speech synthesis interface'
)
source=("${pkgname%-bin}.sh" "vesktop.desktop" "https://raw.githubusercontent.com/Vencord/Vesktop/refs/tags/v${pkgver}/build/icon.svg")
sha256sums=(
    '506c246328af639d6f6a3e52215c7b34af2a6df11d195de6f57a8bbee750cce9'
    '98fa8f661b065c2d825e24f0055a40ae01d58d23628ad7ebf6914296209dd43c'
    '7d0f9dbdb3c86f800577d7fb26a22defeb7025d10c405f634d3c2b939a75be7f'
)
source_aarch64=("${pkgname%-bin}-${pkgver}-arm64.tar.gz::${url}/releases/download/v${pkgver}/${pkgname%-bin}-${pkgver}-arm64.tar.gz")
source_x86_64=("${pkgname%-bin}-${pkgver}.tar.gz::${url}/releases/download/v${pkgver}/${pkgname%-bin}-${pkgver}.tar.gz")
sha256sums_aarch64=('20a13c09fc877c269fba48561164712fa95c6e3fdaf7b4f4d0715616102e089f')
sha256sums_x86_64=('c78489c89caad16ba64c082c1c1095a45b66bc84bb437bda7f7d27c91c6d9907')

case "$CARCH" in
  x86_64)
    _arch=""
    ;;
  aarch64)
    _arch="-arm64"
    ;;
  *)
    _arch="_"
    ;;
esac

package() {
    install -d ${pkgdir}/usr/lib/vesktop
    cp -r "${srcdir}/${pkgname%-bin}-${pkgver}${_arch}/"* "$pkgdir/usr/lib/vesktop/"
    install -Dm644 "${srcdir}/vesktop.desktop" "$pkgdir/usr/share/applications/vesktop.desktop"
    install -Dm755 "${srcdir}/${pkgname%-bin}.sh" "${pkgdir}/usr/bin/${pkgname%-bin}"
    install -Dm644 "${srcdir}/icon.svg" "${pkgdir}/usr/share/icons/hicolor/scalable/apps/${pkgname%-bin}.svg"
}
