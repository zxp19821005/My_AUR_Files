# Maintainer: taotieren <admin@taotieren.com>
pkgname=115-browser-bin
_pkgname=115br
_appname=115Browser
# 版本更新检查页面：https://appversion.115.com/1/web/1.0/api/getMultiVer
pkgver=36.0.0
pkgrel=99
pkgdesc="115 Browser / 115 浏览器"
arch=('x86_64')
url="https://115.com/product_browser"
license=('LicenseRef-scancode-commercial-license')
provides=("${pkgname%-bin}")
conflicts=("${pkgname%-bin}")
options=(
    '!strip'
    '!debug'
)
depends=(
    'expat'
    'libidn2'
    'libcups'
    'openssl-1.1'
    'mesa'
    'nss'
    'at-spi2-core'
    'nspr'
    'alsa-lib'
    'libxrandr'
    'cairo'
    'libxkbcommon'
    'libxdamage'
    'libxcomposite'
    'pango'
)
source=(
    "${pkgname%-bin}-${pkgver}.deb::https://down.115.com/client/115pc/lin/${_pkgname}_v${pkgver}.deb"
    "LICENSE.html::http://115.com/privacy.html"
    "${pkgname%-bin}.sh"
)
sha256sums=('139fb4e36d7f48f1e179317e5ad2bd8b12879e71d3c4ff99da26865661b4fc48'
            'e65199037614ec40eae799f013d007a9252d1b870ec9406fffd7f0aaee3951f1'
            '1bb3391bfe0f0a76d96ae3fcec1d7e981384e9536cec06009dfe5348b4d2b7c9')
prepare() {
    sed -i -e "
        s/@appname@/${pkgname%-bin}/g
        s/@runname@/${_appname}/g
    " "${srcdir}/${pkgname%-bin}.sh"
    bsdtar -xf "${srcdir}/data."*
    sed -i -e "
        /Exec=/d
        /Icon=/d
    " "${srcdir}/usr/share/applications/${_appname}.desktop"
    sed -i -e "
        4i\Exec=${pkgname%-bin}
        5i\Icon=${pkgname%-bin}
    " "${srcdir}/usr/share/applications/${_appname}.desktop"
    chmod 755 "${srcdir}/usr/local/${_appname}/extensions"
}
package() {
    install -Dm755 "${srcdir}/${pkgname%-bin}.sh" "${pkgdir}/usr/bin/${pkgname%-bin}"
    install -Dm755 -d "${pkgdir}/usr/lib/${pkgname%-bin}"
    cp -Pr --no-preserve=ownership "${srcdir}/usr/local/${_appname}/"* "${pkgdir}/usr/lib/${pkgname%-bin}"
    install -Dm644 "${srcdir}/usr/share/applications/${_appname}.desktop" "${pkgdir}/usr/share/applications/${pkgname%-bin}.desktop"
    install -Dm644 "${srcdir}/usr/local/${_appname}/res/${_appname}.png" "${pkgdir}/usr/share/pixmaps/${pkgname%-bin}.png"
    install -Dm644 "${srcdir}/LICENSE.html" -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
