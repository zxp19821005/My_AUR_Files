# Maintainer: Azure Zeng <weedycn at outlook dot com>
# Contributor: Pylogmon <pylogmon@outlook.com>
_appname=clash-verge
pkgname="${_appname}-rev-bin"
_pkgname='Clash Verge'
pkgver=2.4.4
pkgrel=99
pkgdesc="Continuation of Clash Verge | A Clash Meta GUI based on Tauri.(Prebuilt version)"
arch=(
    'aarch64'
    'armv7h'
    'x86_64'
)
url="https://www.clashverge.dev/"
_ghurl="https://github.com/clash-verge-rev/clash-verge-rev"
license=('GPL-3.0-only')
conflicts=(
    "${pkgname%-bin}"
    "${pkgname%-alpha}"
    "${_appname}"
)
provides=("${pkgname%-bin}=${pkgver}")
depends=(
    'webkit2gtk-4.1'
    'gtk3'
    'libayatana-appindicator'
)
options=('!strip')
source_aarch64=("${pkgname%-bin}-${pkgver}-aarch64.rpm::${_ghurl}/releases/download/v${pkgver}/${_pkgname// /.}-${pkgver}-1.aarch64.rpm")
source_armv7h=("${pkgname%-bin}-${pkgver}-armv7h.rpm::${_ghurl}/releases/download/v${pkgver}/${_pkgname// /.}-${pkgver}-1.armhfp.rpm")
source_x86_64=("${pkgname%-bin}-${pkgver}-x86_64.rpm::${_ghurl}/releases/download/v${pkgver}/${_pkgname// /.}-${pkgver}-1.x86_64.rpm")
sha512sums_aarch64=('0bef958944cffda26644acb3f4cae786b154272ff2aa1a5c678ab2bd3913b6c3dba30ce2b58e52b9efa32f654afe5679d6b3a26dc7ee373e2a7568fdb2494a05')
sha512sums_armv7h=('7596e579d9f18bc1839122361d4b79700b0cfd83b70d445a880dd309f3e91b0794f4c1fc7e8e8824994eabfc27d928c5b5119a4b2d6636c3b18f2b115950d116')
sha512sums_x86_64=('b173b0173061b9098b559d86cfa2fd24424c2a77f6b3703b061d614d00f7216296c96b61d33bf4cd4e761a27bd3f5c27b8d70532fd9a6752b4300fdac386242c')
prepare() {
    sed -i -e "
        s/Development/Network/g
        s/Icon=${_appname}/Icon=${pkgname%-bin}/g
    " "${srcdir}/usr/share/applications/${_pkgname}.desktop"
}
package() {
    install -Dm755 "${srcdir}/usr/bin/"* -t "${pkgdir}/usr/bin"
    cp -Pr --no-preserve=ownership "${srcdir}/usr/lib" "${pkgdir}/usr"
    install -Dm644 "${srcdir}/usr/share/applications/${_pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname%-bin}.desktop"
    for _icons in "${_icon_sizes[@]}";do
        install -Dm644 "${srcdir}/usr/share/icons/hicolor/${_icons}/apps/${_appname}.png" \
            "${pkgdir}/usr/share/icons/hicolor/${_icons//@2/}/apps/${pkgname%-bin}.png"
    done
}
