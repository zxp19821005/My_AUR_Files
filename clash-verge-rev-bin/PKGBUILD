# Maintainer: Azure Zeng <weedycn at outlook dot com>
# Contributor: Pylogmon <pylogmon@outlook.com>
_appname=clash-verge
pkgname="${_appname}-rev-bin"
_pkgname='Clash Verge'
pkgver=2.4.0
pkgrel=99
pkgdesc="Continuation of Clash Verge | A Clash Meta GUI based on Tauri.(Prebuilt version)"
arch=(
    'aarch64'
    'armv7h'
    'x86_64'
)
url="https://www.clashverge.dev/"
_ghurl="https://github.com/clash-verge-rev/clash-verge-rev"
license=('GPL-3.0-only')
conflicts=(
    "${pkgname%-bin}"
    "${pkgname%-alpha}"
    "${_appname}"
)
provides=("${pkgname%-bin}=${pkgver}")
depends=(
    'webkit2gtk-4.1'
    'gtk3'
    'libayatana-appindicator'
)
options=('!strip')
source_aarch64=("${pkgname%-bin}-${pkgver}-aarch64.rpm::${_ghurl}/releases/download/v${pkgver}/${_pkgname// /.}-${pkgver}-1.aarch64.rpm")
source_armv7h=("${pkgname%-bin}-${pkgver}-armv7h.rpm::${_ghurl}/releases/download/v${pkgver}/${_pkgname// /.}-${pkgver}-1.armhfp.rpm")
source_x86_64=("${pkgname%-bin}-${pkgver}-x86_64.rpm::${_ghurl}/releases/download/v${pkgver}/${_pkgname// /.}-${pkgver}-1.x86_64.rpm")
sha512sums_aarch64=('3041d576917b926e5e03ded15b72b33e298d51cf277b9e7f5784ee2d729f54d3b825c0a68cc42739711e6b27ded9c9aea9c3d1fa17389ac90855a87495d86850')
sha512sums_armv7h=('6c4cf977a15a656d377fd8686b21e28b65161bb3d8b11758984c6db0ddebcab86d5499683b591ee9bbed1db2f6c2fe1db01b39748a2c8b6c71f48381cadb75f1')
sha512sums_x86_64=('35e18e1e092bbfe0fd4399f3769c9ea77d939008e6250e4e82ff3a26feb4eb08ce4ddc99040178dd80b863781405f38cb27433c942a9a487bd6e2d6a7e16397f')
prepare() {
    sed -i -e "
        s/Development/Network/g
        s/Icon=${_appname}/Icon=${pkgname%-bin}/g
    " "${srcdir}/usr/share/applications/${_pkgname}.desktop"
}
package() {
    install -Dm755 "${srcdir}/usr/bin/"* -t "${pkgdir}/usr/bin"
    cp -Pr --no-preserve=ownership "${srcdir}/usr/lib" "${pkgdir}/usr"
    install -Dm644 "${srcdir}/usr/share/applications/${_pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname%-bin}.desktop"
    for _icons in "${_icon_sizes[@]}";do
        install -Dm644 "${srcdir}/usr/share/icons/hicolor/${_icons}/apps/${_appname}.png" \
            "${pkgdir}/usr/share/icons/hicolor/${_icons//@2/}/apps/${pkgname%-bin}.png"
    done
}
