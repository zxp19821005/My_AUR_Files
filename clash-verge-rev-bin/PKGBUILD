# Maintainer: Azure Zeng <weedycn at outlook dot com>
# Contributor: Pylogmon <pylogmon@outlook.com>
_appname=clash-verge
pkgname="${_appname}-rev-bin"
_pkgname='Clash Verge'
pkgver=2.3.2
pkgrel=99
pkgdesc="Continuation of Clash Verge | A Clash Meta GUI based on Tauri.(Prebuilt version)"
arch=(
    'aarch64'
    'armv7h'
    'x86_64'
)
url="https://www.clashverge.dev/"
_ghurl="https://github.com/clash-verge-rev/clash-verge-rev"
license=('GPL-3.0-only')
conflicts=(
    "${pkgname%-bin}"
    "${pkgname%-alpha}"
    "${_appname}"
)
provides=("${pkgname%-bin}=${pkgver}")
depends=(
    'webkit2gtk-4.1'
    'gtk3'
    'libayatana-appindicator'
)
options=('!strip')
source_aarch64=("${pkgname%-bin}-${pkgver}-aarch64.rpm::${_ghurl}/releases/download/v${pkgver}/${_pkgname// /.}-${pkgver}-1.aarch64.rpm")
source_armv7h=("${pkgname%-bin}-${pkgver}-armv7h.rpm::${_ghurl}/releases/download/v${pkgver}/${_pkgname// /.}-${pkgver}-1.armhfp.rpm")
source_x86_64=("${pkgname%-bin}-${pkgver}-x86_64.rpm::${_ghurl}/releases/download/v${pkgver}/${_pkgname// /.}-${pkgver}-1.x86_64.rpm")
sha512sums_aarch64=('a1458e524ffdea5e7816af07364eb52be63efb59778828dded1d036ddafa3a83084ddac908e96b7a4fd209a19f65734fd7de5f9e355f6145e1bfc54aa0db87b2')
sha512sums_armv7h=('a73c8ceb625b3e3aa93c2a081c7543d1d8fc99c33f150fcb42b35197f1d9d1bb70f4d0f069ed9511bdbb49ee698e358a5536f6689b2e48171e2a083a0cc4a367')
sha512sums_x86_64=('1c1d93a11f5a1591a35494906354f6f306b5cd3e23bed52965907267b34525ca0af7712d194330a1c0358d361746d9058585842f2383a001fe5eee756aca46bb')
prepare() {
    sed -i -e "
        s/Development/Network/g
        s/Icon=${_appname}/Icon=${pkgname%-bin}/g
    " "${srcdir}/usr/share/applications/${_pkgname}.desktop"
}
package() {
    install -Dm755 "${srcdir}/usr/bin/"* -t "${pkgdir}/usr/bin"
    cp -Pr --no-preserve=ownership "${srcdir}/usr/lib" "${pkgdir}/usr"
    install -Dm644 "${srcdir}/usr/share/applications/${_pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname%-bin}.desktop"
    for _icons in "${_icon_sizes[@]}";do
        install -Dm644 "${srcdir}/usr/share/icons/hicolor/${_icons}/apps/${_appname}.png" \
            "${pkgdir}/usr/share/icons/hicolor/${_icons//@2/}/apps/${pkgname%-bin}.png"
    done
}
