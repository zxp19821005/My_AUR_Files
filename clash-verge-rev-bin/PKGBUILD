# Maintainer: Azure Zeng <weedycn at outlook dot com>
# Contributor: Pylogmon <pylogmon@outlook.com>
_appname=clash-verge
pkgname="${_appname}-rev-bin"
_pkgname='Clash Verge'
pkgver=2.4.6
pkgrel=99
pkgdesc="Continuation of Clash Verge | A Clash Meta GUI based on Tauri.(Prebuilt version)"
arch=(
    'aarch64'
    'armv7h'
    'x86_64'
)
url="https://www.clashverge.dev/"
_ghurl="https://github.com/clash-verge-rev/clash-verge-rev"
license=('GPL-3.0-only')
conflicts=(
    "${pkgname%-bin}"
    "${pkgname%-alpha}"
    "${_appname}"
)
provides=("${pkgname%-bin}=${pkgver}")
depends=(
    'webkit2gtk-4.1'
    'gtk3'
    'libayatana-appindicator'
)
options=('!strip')
source_aarch64=("${pkgname%-bin}-${pkgver}-aarch64.rpm::${_ghurl}/releases/download/v${pkgver}/${_pkgname// /.}-${pkgver}-1.aarch64.rpm")
source_armv7h=("${pkgname%-bin}-${pkgver}-armv7h.rpm::${_ghurl}/releases/download/v${pkgver}/${_pkgname// /.}-${pkgver}-1.armhfp.rpm")
source_x86_64=("${pkgname%-bin}-${pkgver}-x86_64.rpm::${_ghurl}/releases/download/v${pkgver}/${_pkgname// /.}-${pkgver}-1.x86_64.rpm")
sha512sums_aarch64=('4ae8e52c5542652a7e63c02c3d21c37c502a41e47db4812a544cba40e9ce1afff628aed9f9c4095e92845134f9f8e1a2497b2ebe534d3b58f7cc000fc3df4946')
sha512sums_armv7h=('c320fbf9504881105549220eb67f19d4156749fe4b2f0c772025bf79a13e045d87cb9cca75de88a6c48dc3ab06bdaba3c63eb9a6bee9581021ff5da36279219c')
sha512sums_x86_64=('3facc2e593de4fc4ea915eb5f8929301eba083f61e776596cf176ad7e5a176ca3fe2abea940ceb360a4d5f985b3f38bb501b84786b77708087d299fb9c6ce704')
prepare() {
    sed -i -e "
        s/Development/Network/g
        s/Icon=${_appname}/Icon=${pkgname%-bin}/g
    " "${srcdir}/usr/share/applications/${_pkgname}.desktop"
}
package() {
    install -Dm755 "${srcdir}/usr/bin/"* -t "${pkgdir}/usr/bin"
    cp -Pr --no-preserve=ownership "${srcdir}/usr/lib" "${pkgdir}/usr"
    install -Dm644 "${srcdir}/usr/share/applications/${_pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname%-bin}.desktop"
    for _icons in "${_icon_sizes[@]}";do
        install -Dm644 "${srcdir}/usr/share/icons/hicolor/${_icons}/apps/${_appname}.png" \
            "${pkgdir}/usr/share/icons/hicolor/${_icons//@2/}/apps/${pkgname%-bin}.png"
    done
}
