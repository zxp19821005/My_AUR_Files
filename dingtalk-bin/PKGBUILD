# Maintainer: zhullyb <zhullyb [at] outlook dot com>
# Maintainer: yjun <jerrysteve1101 at gmail dot com>
# Contributor: Bruce Zhang <zttt183525594@gmail.com>
# Contributor: witt <1989161762 at qq dot com>
pkgname=dingtalk-bin
_debname="com.alibabainc.${pkgname%-bin}"
# https://dtapp-pub.dingtalk.com/dingtalk-desktop/xc_dingtalk_update/linux_deb/Update/other/amd64/linux_dingtalk_update_package_gray.json
pkgver=7.6.45.5062501
pkgrel=99
pkgdesc="DingTalk,钉钉"
arch=(
    'aarch64'
    'x86_64'
)
url="https://www.dingtalk.com/"
_dlurl="https://dtapp-pub.dingtalk.com/dingtalk-desktop/xc_dingtalk_update/linux_deb/Release"
provides=(
    "${_debname}=${pkgver}"
    "${pkgname%-bin}=${pkgver}"
)
conflicts=(
    "${_debname}"
)
replaces=(
    "${_debname}"
)
license=('LicenseRef-custom')
options=(
    '!strip'
)
depends=(
    'glu'
    'gtk2'
    'libxcrypt-compat'
    'qt5-tools'
    'qt5-x11extras'
    'qt5-multimedia'
    'qt5-svg'
    'ffmpeg'
    'gtk3'
    'alsa-lib'
    'nss'
    'nspr'
)
makedepends=(
    'execstack'
)
optdepends=(
    'zenity: fix crashes when downloading files, not required on kde.'
    'libxss: fix tray icon functionality in gnome.'
    'qt5-wayland: needed in wayland'
)
source_aarch64=("${pkgname%-bin}-${pkgver}-aarch64.deb::${_dlurl}/${_debname}_${pkgver}_arm64.deb")
source_x86_64=("${pkgname%-bin}-${pkgver}-x86_64.deb::${_dlurl}/${_debname}_${pkgver}_amd64.deb")
source=(
    "${pkgname%-bin}.svg"
    "${pkgname%-bin}.sh"
)
sha256sums=('b526f6da0d486daeaf52948e0557280b8c8898a20678319e41dd81f0702af73b'
            '3e1ce1cfa5d686e733897fe5cdbc04c2322a240ae11ce1a33b82e6959221bc75')
sha256sums_aarch64=('79b20a15f27cb37c34c865daa53598b4f4b3f1080bb0deae8e3da4f664054769')
sha256sums_x86_64=('e3da6605db9c031532ed9a9083eb5d0eab85791fa525cce9705f26f95be1228f')
prepare() {
    sed -i -e "
        s/@appname@/${pkgname%-bin}/g
        s/@runname@/${_debname}/g
    " "${srcdir}/${pkgname%-bin}.sh"
    bsdtar -xf "${srcdir}/data."*
    sed -i -e "
        s/\/opt\/apps\/${_debname}\/files\/Elevator.sh/${pkgname%-bin}/g
        s/\/opt\/apps\/${_debname}\/files\/logo.ico/${pkgname%-bin}/g
        s/Comment=/Comment=${pkgdesc}/g
        s/Chat/Network/g
    " "${srcdir}/opt/apps/${_debname}/entries/applications/${_debname}.desktop"
    # fix chinese input in workbench
    rm -rf "${srcdir}/opt/apps/${_debname}/files/"*-Release*/libgtk-x11-2.0.so.*
    rm -rf "${srcdir}/opt/apps/${_debname}/files/"*-Release*/{libm.so.6,Resources/{i18n/tool/*.exe,qss/mac,web_content/NativeWebContent_*.zip},libstdc*}
    # remove unused lib
    rm -rf "${srcdir}/opt/apps/${_debname}/files/"*-Release*/{libcurl.so.4,libz*}
    execstack -c "${srcdir}/opt/apps/${_debname}/files/"*-Release*/{"${pkgname%-bin}"_dll,libconference_new}.so
}
package() {
    install -Dm755 "${srcdir}/${pkgname%-bin}.sh" "${pkgdir}/usr/bin/${pkgname%-bin}"
    install -Dm755 -d "${pkgdir}/opt/${pkgname%-bin}/release"
    cp -Pr --no-preserve=ownership "${srcdir}/opt/apps/${_debname}/files/"*-Release*/* "${pkgdir}/opt/${pkgname%-bin}/release"
    install -Dm644 "${srcdir}/opt/apps/${_debname}/files/version" -t "${pkgdir}/opt/${pkgname%-bin}"
    install -Dm644 "${srcdir}/opt/apps/${_debname}/entries/applications/${_debname}.desktop" "${pkgdir}/usr/share/applications/${pkgname%-bin}.desktop"
    install -Dm644 "${srcdir}/${pkgname%-bin}.svg" -t "${pkgdir}/usr/share/icons/hicolor/scalable/apps"
    install -Dm644 "${srcdir}/opt/apps/${_debname}/files/doc/${_debname}/copyright" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
