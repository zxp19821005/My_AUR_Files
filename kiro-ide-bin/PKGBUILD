# Maintainer: zxp19821005 <zxp19821005 at 163 dot com>
_appname=kiro
pkgname="${_appname}-ide-bin"
_pkgname=Kiro
pkgver=0.8.86
_electronversion=37
pkgrel=1
pkgdesc="Agent Programming Platform for Real Software.(Prebuilt version.Use system-wide electron)"
arch=('x86_64')
url="https://kiro.dev"
_dlurl="https://prod.download.desktop.kiro.dev/releases/stable/linux-x64/signed"
license=('LicenseRef-Kiro')
conflicts=("${pkgname%-bin}")
provides=("${pkgname%-bin}=${pkgver}")
depends=(
    "electron${_electronversion}"
    'python-fonttools'
    'libxkbfile'
    'perl'
)
makedepends=(
    'openssl'
)
optdepends=(
    'bash'
    'zsh'
)
options=(
    '!emptydirs'
    '!strip'
)
source=(
    "${pkgname%-bin}-${pkgver}.deb::${_dlurl}/${pkgver}/deb/${pkgname%-bin}-${pkgver}-stable-linux-x64.deb"
    "${pkgname%-bin}-${pkgver}-deb-signature.bin::${_dlurl}/${pkgver}/deb/signature.bin"
    "${pkgname%-bin}-certificate.pem::${_dlurl}/${pkgver}/deb/certificate.pem"
    "${pkgname%-bin}.js"
    "${pkgname%-bin}.sh"
)
sha256sums=('c9106ea33e60467619e5a655397fd3cf26289c91d05efd0259a632834f0b1419'
            '10759d3516bbd666b2a333341e8353790d145a30de767ca6d48b837aa7129ad7'
            '2d4d8c0bf5cb6b2694911d6f448a2b9df76eed04683958829ae61fcff10143fd'
            '830fe101e4e16f718ccb2c5e83f9f088676679bf2e4e855b141f545e0085c18a'
            'e0ab2fe87491fabd9c7886f22c6929169edb508be832036a02698760b721f207')
_get_electron_version() {
    _elec_ver="$(strings "${srcdir}/usr/share/${_appname}/${_appname}" | grep '^Chrome/[0-9.]* Electron/[0-9]' | cut -d'/' -f3 | cut -d'.' -f1)"
    echo -e "The electron version is: \033[1;31m${_elec_ver}\033[0m"
}
verify() {
    cd "${SRCDEST}"
    openssl x509 -pubkey -noout -in "${pkgname%-bin}-certificate.pem" > kiro-pubkey.pem
    openssl dgst -sha256 -verify kiro-pubkey.pem -signature "${pkgname%-bin}-${pkgver}-deb-signature.bin" \
        "${pkgname%-bin}-${pkgver}.deb"
}
prepare() {
    _get_electron_version
    sed -i -e "
        s/@electronversion@/${_electronversion}/g
        s/@appname@/${pkgname%-bin}/g
        s/@runname@/app/g
        s/@cfgdirname@/${_pkgname}/g
        s/@options@/env ELECTRON_OZONE_PLATFORM_HINT=auto/g
    " "${srcdir}/${pkgname%-bin}.sh"
    bsdtar -xf "${srcdir}/data."*
    sed -i "s/@ELECTRON@/electron${_electronversion}/g" "${srcdir}/${pkgname%-bin}.js"
    sed -i -e "
        s/\/usr\/share\/${_appname}\/${_appname}/${pkgname%-bin}/g
        s/Icon=code-oss/Icon=${pkgname%-bin}/g
    " "${srcdir}/usr/share/applications/${_appname}"*.desktop
    sed -i "s/${_appname}.desktop/${pkgname%-bin}.desktop/g" "${srcdir}/usr/share/appdata/${_appname}.appdata.xml"
}
package() {
    install -Dm755 "${srcdir}/${pkgname%-bin}.sh" "${pkgdir}/usr/bin/${pkgname%-bin}"
    install -Dm755 "${srcdir}/${pkgname%-bin}.js" -t "${pkgdir}/usr/lib/${pkgname%-bin}"
    cp -Pr --no-preserve=ownership "${srcdir}/usr/share/${_appname}/resources/app/"* "${pkgdir}/usr/lib/${pkgname%-bin}"
    install -Dm644 "${srcdir}/usr/share/pixmaps/code-oss.png" "${pkgdir}/usr/share/pixmaps/${pkgname%-bin}.png"
    install -Dm644 "${srcdir}/usr/share/applications/${_appname}.desktop" "${pkgdir}/usr/share/applications/${pkgname%-bin}.desktop"
    install -Dm644 "${srcdir}/usr/share/applications/${_appname}-url-handler.desktop" "${pkgdir}/usr/share/applications/${pkgname%-bin}-url-handler.desktop"
    install -Dm644 "${srcdir}/usr/share/appdata/${_appname}.appdata.xml" "${pkgdir}/usr/share/appdata/${pkgname%-bin}.appdata.xml"
    install -Dm644 "${srcdir}/usr/share/mime/packages/${_appname}-workspace.xml" "${pkgdir}/usr/share/mime/packages/${pkgname%-bin}-workspace.xml"
    if [ -x "/usr/bin/zsh" ];then
        install -Dm644 "${srcdir}/usr/share/bash-completion/completions/${_appname}" "${pkgdir}/usr/share/bash-completion/completions/${pkgname%-bin}"
    fi
    if [ -x "/usr/bin/zsh" ];then
        install -Dm644 "${srcdir}/usr/share/zsh/vendor-completions/_${_appname}" "${pkgdir}/usr/share/zsh/vendor-completions/_${pkgname%-bin}"
    fi
    install -Dm644 "${srcdir}/usr/share/${_appname}/resources/app/LICENSE.txt" -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
